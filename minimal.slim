initialize() {
	initializeSLiMModelType("nonWF");
	initializeSLiMOptions(dimensionality="xy");
	defaults = Dictionary(
		"seed", getSeed(),
		"SD", 0.3, // sigma_D, dispersal distance
		"SI", 0.3, // sigma_I, interaction distance for competition	
		"SM", 0.3, // sigma_M, mate choice distance
		"K", 5, // carrying capacity per unit area
		"LIFETIME", 4, // average life span
		"WIDTH", 25.0, // width of the simulated area
		"HEIGHT", 25.0, // height of the simulated area
		"RUNTIME", 200, // total number of ticks to run the simulation for
		"L", 1e8, // genome length
		"R", 1e-8, // recombination rate
		"MU", 0, // mutation rate
		"OUTDIR", exists("OUTDIR") ? OUTDIR else ".",
		"PARAMFILE", exists("PARAMFILE") ? PARAMFILE else "./params.json"
		);
	
	setupParams(defaults);
	
	// Set up parameters that depend on other parameters and add it to parameter dictionary
	
	defineConstant("FECUN", 1 / LIFETIME);
	defaults.addKeysAndValuesFrom(Dictionary("FECUN", FECUN));
	defineConstant("RHO", FECUN / ((1 + FECUN) * K));
	defaults.addKeysAndValuesFrom(Dictionary("RHO", RHO));
	defineGlobal("PARAMS", defaults);
	
	setSeed(seed);
	
	initializeMutationRate(MU);
	initializeMutationType("m1", 0.5, "f", 0.0);
	initializeGenomicElementType("g1", m1, 1.0);
	initializeGenomicElement(g1, 0, L-1);
	initializeRecombinationRate(R);
	
	// spatial interaction for local competition
	initializeInteractionType(1, "xy", reciprocal=T, maxDistance=3 * SI);
	i1.setInteractionFunction("n", 1., SI);
	// mate choice
	initializeInteractionType(2, "xy", reciprocal=T, maxDistance=3 * SM);
	i2.setInteractionFunction("n", 1., SM);
}

1 first() {
	sim.addSubpop("p1", asInteger(K*WIDTH*HEIGHT));
	p1.setSpatialBounds(c(0, 0, WIDTH, HEIGHT));
	p1.individuals.setSpatialPosition(p1.pointUniform(p1.individualCount));
	community.rescheduleScriptBlock(s1, ticks=RUNTIME);
}

first() {
	// "preparation for the reproduction() callback".
	i2.evaluate(p1);
}

reproduction(){
	mate = i2.drawByStrength(individual, 1);
	if (mate.size()) {
		offsprings = subpop.addCrossed(individual, mate, count=rpois(1, FECUN));
	}
}

early() {
	// Disperse offsprings
	offsprings = p1.subsetIndividuals(maxAge = 0);
	pos = offsprings.spatialPosition;
	pos = p1.pointDeviated(offsprings.size(), pos, "reprising", INF, "n", SD);
	offsprings.setSpatialPosition(pos);
	
	i1.evaluate(p1);
	inds = p1.individuals;
	competition = i1.localPopulationDensity(inds);
	inds.fitnessScaling = 1 / (1 + RHO * competition);
}

late() {
	if (p1.individualCount == 0) {
		catn("Population went extinct! Ending the simulation.");
		sim.simulationFinished();
	}
}

s1 late() {
	catn("End of simulation (run time reached)");
	sim.simulationFinished();
}


function (void)setupParams(object<Dictionary>$ defaults)
{
	if (fileExists(defaults.getValue("PARAMFILE"))){
		local_defaults = Dictionary(paste(readFile(defaults.getValue("PARAMFILE")), sep="\n"));
		defaults.addKeysAndValuesFrom(local_defaults);
		defaults.setValue("read_from_paramfile", defaults.getValue("PARAMFILE"));
	}
	defaults.setValue("OUTBASE", defaults.getValue("OUTDIR") + "/out_" +	defaults.getValue("seed"));
	defaults.setValue("OUTPATH", defaults.getValue("OUTBASE") + ".trees");
	
	catn("-------------\n");
	for (k in defaults.allKeys) {
		if (!exists(k)) {
			defineConstant(k, defaults.getValue(k));
		} else {
			defaults.setValue(k, executeLambda(paste(c(k, ";"), sep='')));			}
		catn("  " + k + ": " + defaults.getValue(k));
	}
	catn("-------------\n");
}