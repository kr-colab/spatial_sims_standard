initialize() {
	initializeSLiMModelType("nonWF");
	initializeSLiMOptions(dimensionality="xy");
	
	defaults = Dictionary(
		"SEED", getSeed(),
		"SD", 0.3,		// sigma_D, dispersal distance
		"SX", 0.3,		// sigma_X, interaction distance for measuring local density
		"SM", 0.3,		// sigma_M, mate choice distance
		"K", 5,			// carrying capacity per unit area
		"LIFETIME", 4,		// average life span
		"WIDTH", 25.0,		// width of the simulated area
		"HEIGHT", 25.0,		// height of the simulated area
		"RUNTIME", 300,		// total number of ticks to run the simulation for
		"OUTPUTTIME", c(200, 250, 299),		// when to save density information
		"L", 1e8,		// genome length
		"R", 1e-8,		// recombination rate
		"MU", 0,		// mutation rate
		"MIN_L", 1.5,		// minimal map value
		"MAX_L", 3.0,		// maximum map value
		"IMAGE_LOCATION", "./practice_maps/mountain_height.png"
		);
	
	// Set up parameters with a user-defined function
	setupParams(defaults);
	
	// Set up constants that depend on externally defined parameters
	defineConstant("FECUN", 1 / LIFETIME);
	defineConstant("RHO", FECUN / ((1 + FECUN) * K));
	defineConstant("PARAMS", defaults);
	
	setSeed(SEED);
	
	// basic neutral genetics
	initializeMutationRate(MU);
	initializeMutationType("m1", 0.5, "f", 0.0);
	initializeGenomicElementType("g1", m1, 1.0);
	initializeGenomicElement(g1, 0, L-1);
	initializeRecombinationRate(R);
	
	// spatial interaction for local density measurement
	initializeInteractionType(1, "xy", reciprocal=T, maxDistance=3 * SX);
	i1.setInteractionFunction("n", 1, SX);
	
	// spatial interaction for mate choice
	initializeInteractionType(2, "xy", reciprocal=T, maxDistance=3 * SM);
	i2.setInteractionFunction("n", 1, SM);
}

1 first() {
	sim.addSubpop("p1", asInteger(K * WIDTH * HEIGHT));
	p1.setSpatialBounds(c(0, 0, WIDTH, HEIGHT));
	p1.individuals.setSpatialPosition(p1.pointUniform(p1.individualCount));
	mapImage = Image(IMAGE_LOCATION); // adding the image
	map_vals = (1 - mapImage.floatK) *(MAX_L - MIN_L) + MIN_L; // defining values
	map = p1.defineSpatialMap("world", "xy", map_vals, valueRange=c(MIN_L, MAX_L), colors=c('#FFFFFF', '#000000'));
	defineConstant("MAP", map);
}

first() {
	// preparation for the reproduction() callback
	i2.evaluate(p1);
}

reproduction() {
	mate = i2.drawByStrength(individual, 1);
	if (mate.size())
		subpop.addCrossed(individual, mate, count=rpois(1, FECUN));
}

early() {
	// Disperse offspring
	offspring = p1.subsetIndividuals(maxAge=0);
	p1.deviatePositions(offspring, "reprising", INF, "n", SD);

	// Measure local density and use it for density regulation
	i1.evaluate(p1);
	inds = p1.individuals;
	competition = i1.localPopulationDensity(inds);
	local_l = MAP.mapValue(inds.spatialPosition);
	inds.fitnessScaling = 1/(1 + (RHO * competition / local_l));
}

late() {
	if (p1.individualCount == 0) {
		catn("Population went extinct! Ending the simulation.");
		sim.simulationFinished();
	}
}

OUTPUTTIME late() {
	// Output density and relative map value of each individual ("pixial", between 0 and 1)
	inds = p1.individuals;
	i1.evaluate(p1);
	ind_den = i1.localPopulationDensity(inds);
	local_l = MAP.mapValue(inds.spatialPosition);
	pixial = (local_l - MIN_L) / (MAX_L - MIN_L);

	mytextfile = OUTBASE+"_by_ind_Ind_values_max_"+MAX_L+"_min_l_"+MIN_L +"_.text";
	mytextfileinfo = NULL;
	// printing out information at selected ticks
	for(i in seq(0, len(pixial))) {
		catn(pixial[i]);

	// writing info about density
		mytextfileinfo = c(mytextfileinfo, paste0(sim.cycle + "," + ind_den[i] + "," + pixial[i]));
	}
	writeFile(mytextfile, mytextfileinfo, append=T);
}

RUNTIME late() {
	catn("End of simulation (run time reached)");
	sim.simulationFinished();
}



function (void)setupParams(object<Dictionary>$ defaults)
{
	if (!exists("PARAMFILE")) defineConstant("PARAMFILE", "./params.json");
	if (!exists("OUTDIR")) defineConstant("OUTDIR", ".");
	defaults.addKeysAndValuesFrom(Dictionary("PARAMFILE", PARAMFILE, "OUTDIR", OUTDIR));
	
	if (fileExists(PARAMFILE)) {
		defaults.addKeysAndValuesFrom(Dictionary(readFile(PARAMFILE)));
		defaults.setValue("READ_FROM_PARAMFILE", PARAMFILE);
	}
	
	defaults.setValue("OUTBASE", OUTDIR + "/out_" +	defaults.getValue("SEED"));
	
	for (k in defaults.allKeys) {
		if (!exists(k))
			defineConstant(k, defaults.getValue(k));
		else
			defaults.setValue(k, executeLambda(k + ";"));
	}
	
	// print out default values
	catn("===========================");
	catn("Model constants: " + defaults.serialize("pretty"));
	catn("===========================");
}